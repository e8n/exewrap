apply plugin: 'java'

archivesBaseName = 'jetty'
version = '9.4.28'

tasks.withType(JavaCompile) {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.compilerArgs << '-Xlint:all,-rawtypes,-serial,-unchecked'
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources', 'src/main/java' ]
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "javax.xml.bind:jaxb-api:2.3.1"
	implementation "org.glassfish.jaxb:jaxb-runtime:2.3.1"
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

defaultTasks 'clean', 'exewrap_x86', 'exewrap_x64'

jar {
	manifest {
		attributes "Main-Class" : "exewrap.jetty.Main"
		from "src/main/resources/META-INF/MANIFEST.MF"
	}
	include "exewrap/**", "org/**"
}

task copyJAXB(type: Copy) {
	setGroup("build")
	setDescription("Copy JAXB libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/ext/jaxb/"
}

task exewrap_x86(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/etc/exewrap.exe"
	args "-A", "x86",
		"-s",
		"-t", "1.8",
		"-L", ";",
		"-b", "-agentlib:jdwp=transport=dt_socket,address=localhost:8000,server=y,suspend=n,quiet=y",
		"-e", "NOLOG;NOHELP",
		"-j", "${jar.archiveFile.get()}",
		"-o", "${buildDir}/x86/${archivesBaseName}.exe",
		"-i", "src/main/resources/images/jetty.ico",
		"-d", "Jetty Windows Service",
		"-p", "jetty",
		"-V", "${version}",
		"-v", "${version}"
}

task exewrap_x64(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/etc/exewrap.exe"
	args "-A", "x64",
		"-s",
		"-t", "1.8",
		"-L", ";",
		"-b", "-agentlib:jdwp=transport=dt_socket,address=localhost:8000,server=y,suspend=n,quiet=y",
		"-e", "NOLOG;NOHELP",
		"-j", "${jar.archiveFile.get()}",
		"-o", "${buildDir}/x64/${archivesBaseName}.exe",
		"-i", "src/main/resources/images/jetty.ico",
		"-d", "Jetty Windows Service",
		"-p", "jetty",
		"-V", "${version}",
		"-v", "${version}"
}
